import java.text.SimpleDateFormat

apply plugin: 'com.android.application'

/**
 * Put release keystore as `release-key.jks` and the signing information
 * as `release-key.properties` on project directory
 * (both files already listed on project .gitignore).
 *
 * The `release-key.properties` contains:
 *
 *      storePassword=****
 *      keyAlias=alias_name
 *      keyPassword=****
 *
 *  Then build release apk with
 *
 *      gradle clean assembleRelease
 *
 *  The signed apk should be in the `app/build/outputs/apk` directory.
 *
 *  Both release key and signing information should not committed
 *  to the git repository.
 *
 */
def keyStoreFile = rootProject.file("release-key.jks")
def keyPropsFile = rootProject.file("release-key.properties")

def keyProps = new Properties()
if (keyPropsFile.exists()) {
    keyPropsFile.withInputStream { stream ->
        keyProps.load(stream)
    }
}

def dateFormat = new SimpleDateFormat('yyyyMMddHHmm')
def developmentVersionSuffix = dateFormat.format(new Date())


android {
    compileSdkVersion 28
    defaultConfig {
        applicationId "io.github.easyintent.quickref"
        minSdkVersion 16
        targetSdkVersion 27
        versionCode 7
        versionName "0.6.0"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        javaCompileOptions {
            annotationProcessorOptions {
                arguments = [
                    "classSuffix": "Ex",
                    "resourcePackageName": android.defaultConfig.applicationId
                ]
            }
        }
    }

    signingConfigs {
        release {
            storeFile keyStoreFile
            storePassword keyProps['storePassword']
            keyAlias keyProps['keyAlias']
            keyPassword keyProps['keyPassword']
        }
    }

    buildTypes {
        debug {
            minifyEnabled false
            versionNameSuffix "-debug"
        }
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'

            // see signingConfig above
            signingConfig signingConfigs.release

            // comment below line when actually release new version
            versionNameSuffix "-git-$developmentVersionSuffix"
        }
    }

    packagingOptions {
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/NOTICE.txt'
    }

    testOptions {
        unitTests.includeAndroidResources = true
        unitTests.returnDefaultValues = true
        unitTests.all {
            testLogging {
                showStandardStreams = true
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

def supportVersion = '28.0.0'
def aaVersion = '4.6.0'

dependencies {
    implementation 'androidx.appcompat:appcompat:1.0.0'
    implementation 'com.google.android.material:material:1.0.0'
    implementation 'androidx.cardview:cardview:1.0.0'

    implementation "org.androidannotations:androidannotations-api:$aaVersion"
    implementation ('commons-io:commons-io:2.4') {  transitive = false  }
    implementation 'eu.lp0.slf4j:slf4j-android:1.7.22-0'

    annotationProcessor "org.androidannotations:androidannotations:$aaVersion"

    testImplementation 'junit:junit:4.12'

    androidTestImplementation 'androidx.test.ext:junit:1.1.1'
    androidTestImplementation 'androidx.test:runner:1.2.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.2.0'
}
